include "pipelines/common.glsl"

compute_shader [[
	struct Indirect {
		uint vertex_count;
		uint instance_count;
		uint first_index;
		uint base_vertex;
		uint base_instance;
	};
	
	layout(local_size_x = 16, local_size_y = 16) in;

	layout(binding = 0, std430) writeonly buffer OutData {
		Indirect b_indirect;
		float padding0;
		float padding1;
		float padding2;
		vec4 b_data[];
	};
	layout (binding = 2) uniform sampler2D u_heightmap;
	layout (binding = 3) uniform sampler2D u_splatmap;

	layout(std140, binding = 4) uniform Grass {
		vec4 u_pos;
		vec4 u_lod_ref_point;
		ivec2 u_from;
		ivec2 u_to;
		vec2 u_terrain_size;
		float u_terrain_y_scale;
		float u_distance;
		uint u_step;
		float u_grass_height;
		uint u_vertex_count;
		uint u_type;
		float u_radius;
		uint u_rotation_mode;
		vec2 u_terrain_xz_scale;
	};

	vec3 permute(vec3 x) { return mod(((x*34.0)+1.0)*x, 289.0); }

	// https://gist.github.com/patriciogonzalezvivo/670c22f3966e662d2f83
	float snoise(vec2 v){
	  const vec4 C = vec4(0.211324865405187, 0.366025403784439,
			   -0.577350269189626, 0.024390243902439);
	  vec2 i  = floor(v + dot(v, C.yy) );
	  vec2 x0 = v -   i + dot(i, C.xx);
	  vec2 i1 = vec2(0.0, 1.0);
	  if (x0.x > x0.y) i1 = vec2(1.0, 0.0);
	  vec4 x12 = x0.xyxy + C.xxzz;
	  x12.xy -= i1;
	  i = mod(i, 289.0);
	  vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
	  + i.x + vec3(0.0, i1.x, 1.0 ));
	  vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy),
		dot(x12.zw,x12.zw)), 0.0);
	  m = m*m ;
	  m = m*m ;
	  vec3 x = 2.0 * fract(p * C.www) - 1.0;
	  vec3 h = abs(x) - 0.5;
	  vec3 ox = floor(x + 0.5);
	  vec3 a0 = x - ox;
	  m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
	  vec3 g;
	  g.x  = a0.x  * x0.x  + h.x  * x0.y;
	  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
	  return 130.0 * dot(m, g);
	}
	
	void main() {
		uint id = gl_GlobalInvocationID.y * gl_WorkGroupSize.x + gl_GlobalInvocationID.x;
		
		vec3 inst_pos = vec3(0);
		uvec2 ij = u_from + gl_GlobalInvocationID.xy * u_step;
		inst_pos.xz = vec2(ij) * 0.01;
		
		if (any(lessThan(inst_pos.xz, vec2(0)))) return;
		vec2 uv = (inst_pos.xz / (u_terrain_size + u_terrain_xz_scale)) + 0.5 / (u_terrain_size.x / u_terrain_xz_scale + 1);
		uvec4 splat = uvec4(texture(u_splatmap, uv) * 255.0 + 0.5);

		uint mask = (splat.w << 8) | splat.z;
		
		if ((mask & (1 << u_type)) != 0) {
			float r = snoise(ij) * M_PI;
			inst_pos.x += (fract(r * 2531) * 2 - 1) * 0.99 * u_step * 0.01;
			inst_pos.z += (fract(r * 2819) * 2 - 1) * 0.99 * u_step * 0.01;
			
			vec2 uv = (inst_pos.xz / (u_terrain_size + u_terrain_xz_scale)) + 0.5 / (u_terrain_size.x / u_terrain_xz_scale + 1);
			inst_pos.y += texture(u_heightmap, uv).x * u_terrain_y_scale;

			float dist_t = saturate(length(inst_pos + u_lod_ref_point.xyz) / u_distance);
			float scale = 1 - dist_t * dist_t * dist_t;
			scale *= snoise(vec2(ij) * 0.01) * 0.3 + 0.7;
			inst_pos += u_pos.xyz;
			vec4 cullp = vec4(inst_pos, 1);
			for (int i = 0; i < 6; ++i) {
				if (dot(Pass.camera_planes[i], cullp) < -u_radius * scale) {
					return;
				}
			}

			if (scale > 0.01) {
				uint i = atomicAdd(b_indirect.instance_count, 1);
				b_data[i * 2 + 1] = vec4(inst_pos, scale);
				switch(u_rotation_mode) {
					case 1: 
						vec3 axis = normalize(vec3(fract(r * 2531), fract(r * 2819), fract(r * 13)) * 2 - 1);
						b_data[i * 2] = vec4(axis, cos(r*16));
						break;
					default:
					case 0: 
						b_data[i * 2] = vec4(0, sin(r*16), 0, cos(r*16));
						break;
				}
			}
		}
	}
]]